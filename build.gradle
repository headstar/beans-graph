apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.4"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

ext.javaProjects = subprojects

configure(javaProjects) {
    apply from: "$rootDir/gradle/javaprojects.gradle"
    apply from: "$rootDir/gradle/archive.gradle"
    apply from: "$rootDir/gradle/cobertura.gradle"
}

allprojects {
    repositories {
        mavenCentral()
    }


}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.6'
    }
}

def childTestTasks = []
def coverageDatafiles = []
javaProjects.each {
    coverageDatafiles << it.file("build/cobertura/cobertura.ser")
    childTestTasks << "${it.name}:test"
}

cobertura {
    coverageMergeDatafiles = coverageDatafiles
    coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
}

test.dependsOn(childTestTasks)